# -*- mode: gdb-script; -*-
#
# My .gdbinit
#

# Always have history
set history filename ~/.gdb_history
set history save on
set history expansion on
set history size 1000

# I have tmux to deal with pagination
set pagination off

# disable some signal handlers I hit a lot in QEMU
handle SIGUSR1 nostop noprint

#
# A simple instruction stepping macro that steps and then dumps the
# next few instructions
#
define i
    si
    x/3i $pc
end

#
# Alias for register information
#
define regs
    info registers
end

#
# Mono Debugger Helpers
#

define mono_backtrace
 select-frame 0
 set $i = 0
 while ($i < $arg0)
#   set $foo = mono_pmip ($pc)
   set $foo = print_method_from_ip ($pc)
   if ($foo == 0x00)
     frame
   else
     printf "#%d %p in %s\n", $i, $pc, $foo
   end
   up-silently
   set $i = $i + 1
 end
end

define mono_stack
 set $mono_thread = mono_thread_current ()
 if ($mono_thread == 0x00)
   printf "No mono thread associated with this thread\n"
 else
   set $ucp = malloc (sizeof (ucontext_t))
   call (void) getcontext ($ucp)
   call (void) mono_print_thread_dump ($ucp)
   call (void) free ($ucp)
 end
end

# Enable auto-loading for work stuff

set auto-load safe-path /usr/share/gdb:~/lsrc


echo Executed .gdbinit
echo \n
